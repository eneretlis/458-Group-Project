%  PT Parser Version 5.1
%
% Date:  30 Jun 1980
%        (Revised v5.1 - 8 Feb 2021)
%
% Author:  Alan Rosselet
%
% Abstract
%     This module parses the syntax token stream emitted by the scanner
%     and emits a postfix stream of tokens to the semantic analyzer
%     module.  The parser detects and repairs syntax errors so that the
%     token stream emitted to the semantic analyzer is syntactically
%     legal (except for the special case of declaration phase errors - 
%     see the comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order

        pDiv                    'div'
        firstKeywordToken = pDiv
        pMod                    'mod'
        pOr                     'or'
        pAnd                    'and'
        pThen                   'then'
        pElse                   'else'
        pOf                     'of'
        pEnd                    'end'
        pDo                     'do'
        pArray                  'array'
        pFile                   'file'
        pVar                    'var'
        pType                   'type'
        pIf                     'if'
        pCase                   'case'
        pRepeat                 'repeat'
        pWhile                  'while'
        pUsing                  'using'
        pVal                    'val'
        pDef                    'def'
        pUnless                 'unless'
        pElsif                  'elsif'
        pBreak                  'break'
        pWhen                   'when'
        pModule                 'module'
        pReturn                 'return'
        lastKeywordToken = pReturn 

        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '!='
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        pLeftBrace              '{'
        pRightBrace             '}'
        pDotDot                 '..'
        pDoubleEquals           '=='
        pQuestion               '?'
        pDollar                 '$'
        pHash                   '#'
        pNot                    '!'
        lastSyntaxToken = pNot;

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral

        sProgram
        sParmBegin
        sParmEnd
        sConst
        sType
        sVar
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sElsif
        sDo
        sWhileStmt
        sRepeatStmt
        sRepeatEnd
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sNewLine
        sEndOfFile
        sPublic
        sModule
        sDoStmt
        sBreakIf
        sSubstring
        sLength
        sIndex
        lastSemanticToken = sIndex;


rules

Program :
        'using'  .sProgram
        % program name

        % program parameters
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }
        .sParmEnd
        @Block;

Block :
        % Strictly speaking, standard Pascal requires that declarations
        % appear only in the order consts, types, vars, procedures.
        % We allow arbitrary intermixing of these instead to preserve
        % programmer sanity.
        .sBegin
        {[
            | 'val':
                @ConstantDefinitions
            | 'type':
                @TypeDefinitions
            | 'var':
                @VariableDeclarations
            | 'def':
                @DefStmt
            | 'module':
                @ModuleStmt
            | pIdentifier:
                @AssignmentOrCallStmt
            | 'if':
                @IfStmt
            | 'case':
                @CaseStmt
            | 'unless':
                @UnlessStmt
            | 'do':
                @DoStmt
            | 'while':
                @WhileStmt
            | ';':
                .sNullStmt
            | *:
                >
        ]}
        .sEnd;

ConstantDefinitions :
        % Accept one named constant definitions
        .sConst
        pIdentifier  .sIdentifier
        '=' @ConstantValue ;

ConstantValue :
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pStringLiteral:
                .sStringLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

TypeDefinitions :
        % Accept one or more named type definitions.
        .sType
        pIdentifier  .sIdentifier
        ':'  @ValueOrQuby;

ValueOrQuby :
        [
            | pIdentifier:
                .sIdentifier
        ];

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

VariableDeclarations :
        % Accept one or more variable declarations.
        .sVar
        pIdentifier  .sIdentifier
        {[
            | ',':
                .sVar
                pIdentifier  .sIdentifier
            | *:
                >
        ]}
        ':'  @ValueOrQuby;

ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
        '('
        [
            | ')':
            | *:
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    ':'
                    % type identifier
                    pIdentifier  .sIdentifier
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
        ]
        .sParmEnd;

AssignmentOrCallStmt :
        [
            | '=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                @Expression
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
                ']'  '='
                @Expression
                .sExpnEnd
            | *:
                @CallStmt
        ];

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statement rule
        % accept zero or more actual parameters
        [
            | '(':
                {
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:        % no actual parameters
        ]
        .sParmEnd;

IfStmt :
        .sIfStmt
        @Expression
        .sExpnEnd
        'then'  .sThen
        @Block
        {[
            | 'else':
                .sElse
                @Block
                >
            | 'elsif':
                .sElsif
                @Expression
                .sExpnEnd
                'then' .sThen
                @Block
            | *:
                >
        ]};

UnlessStmt :
        .sIfStmt
        @Expression
        .sNot
        .sExpnEnd
        'then'  .sThen
        @Block
        [
            | 'else':
                .sElse
                @Block
            | *:
        ]
        'end';

CaseStmt :
        .sCaseStmt
        @Expression
        .sExpnEnd
        {[
            | 'when':
                @CaseAlternative
            | *:
                >
        ]}
        [
            | 'else':
                .sElse @Block
            | *:
        ]
        'end'
        .sCaseEnd;

CaseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants

        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        'then'
        @Block;

WhileStmt :
        .sWhileStmt
        @Expression
        .sExpnEnd
        'do'
        @Block
        'end';

DoStmt :
        .sDoStmt
        @Block
        'break' 'if' .sBreakIf 
        @Expression
        .sExpnEnd
        @Block
        'end';

DefStmt :
        .sProcedure
        % procedure name
        [
            | '*':
                pIdentifier  .sIdentifier .sPublic
            | *:
                pIdentifier  .sIdentifier
        ]
        @ProcedureHeading
        @Block
        'end';

ModuleStmt :
        .sModule
        pIdentifier .sIdentifier
        @Block
        'end';


Expression :
        @SimpleExpression
        [
            | '!=':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | '==':
                @SimpleExpression .sEq
            | *:
        ];
SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | *:
                >
        ]};

Term :
        @HigherFactor
        {[
            | '*':
                @HigherFactor  .sMultiply
            | 'div':
                @HigherFactor  .sDivide
            | 'mod':
                @HigherFactor  .sModulus
            | 'and':
                .sInfixAnd  @HigherFactor  .sAnd
            |'?':
                @HigherFactor .sIndex
            | *:
                >
        ]};

HigherFactor:
           @Factor
           {[
                | '$':
                    @Factor '..' @Factor
                | *:
                    >
           ]};

Factor :
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | '!':
                @Factor
                .sNot
            | '#':
                @Factor
                .sLength
            | pStringLiteral:
                .sStringLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
